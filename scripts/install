#!/usr/bin/env zsh
set -euo pipefail

root="$(cd "$(dirname "$0")/.." && pwd)"
cd "$root"

version="$(/usr/libexec/PlistBuddy -c 'Print :CFBundleShortVersionString' sources/info.plist)"
app="$root/dist/Section.app"
contents="$app/Contents"
macos="$contents/MacOS"
resources="$contents/Resources"
stage="$root/dist/stage"
dmg="$root/dist/section-$version.dmg"
rw="$root/dist/section-$version-rw.dmg"
volume="Section $version"

swift build -c release
./scripts/assets

rm -rf "$app" "$stage" "$dmg" "$rw"
mkdir -p "$macos" "$resources" "$stage"
cp .build/release/section "$macos/section"
cp sources/info.plist "$contents/Info.plist"
cp dist/SectionIcon.icns "$resources/SectionIcon.icns"
printf 'APPL????' > "$contents/PkgInfo"
chmod +x "$macos/section"

if [[ -n "${SIGN_IDENTITY:-}" ]]; then
	codesign --force --deep --options runtime --sign "$SIGN_IDENTITY" "$app"
else
	codesign --force --deep --sign - "$app"
fi

cp -R "$app" "$stage/"
ln -s /Applications "$stage/Applications"
hdi="/usr/bin/hdiutil"
osas="/usr/bin/osascript"

"$hdi" create -volname "$volume" -srcfolder "$stage" -ov -format UDRW "$rw" >/dev/null

attach="$("$hdi" attach -readwrite -noverify -noautoopen "$rw")"
device="$(echo "$attach" | awk '/^\/dev\// {print $1; exit}')"
mount="$(echo "$attach" | awk '/\/Volumes\// {print substr($0, index($0, "/Volumes/")); exit}')"
diskname="$(basename "$mount")"

"$osas" <<EOF
tell application "Finder"
  tell disk "$diskname"
    open
    set current view of container window to icon view
    set toolbar visible of container window to false
    set statusbar visible of container window to false
    set bounds of container window to {120, 120, 1020, 680}
    set opts to icon view options of container window
    set arrangement of opts to not arranged
    set icon size of opts to 128
    set text size of opts to 14
    set background color of opts to {8704, 9216, 10240}
    set position of item "Section.app" of container window to {250, 300}
    set position of item "Applications" of container window to {650, 300}
    set the extension hidden of item "Section.app" to true
    close
    open
    update without registering applications
    delay 1
  end tell
end tell
EOF

for item in "$mount/.fseventsd" "$mount/.Trashes" "$mount/.TemporaryItems" "$mount/.Spotlight-V100"; do
	rm -rf "$item" >/dev/null 2>&1 || true
done

chflags hidden "$mount/.DS_Store" >/dev/null 2>&1 || true
xcrun SetFile -a V "$mount/.DS_Store" >/dev/null 2>&1 || true
rm -rf "$mount/.background" >/dev/null 2>&1 || true

"$hdi" detach "$device" -quiet
"$hdi" convert "$rw" -ov -format UDZO -imagekey zlib-level=9 -o "$dmg" >/dev/null
rm -f "$rw"
rm -rf "$stage"

echo "$dmg"
